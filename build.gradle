plugins {
    id "java"
    id "io.freefair.lombok" version "4.1.6"
    id "io.qameta.allure" version "2.8.1"
    id "org.gradle.test-retry" version "1.1.9"
    id 'com.adarshr.test-logger' version '2.1.1'
}

group 'automationpractice-demo'
version '1.0.0'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    jcenter()
}

ext {
    selenideVersion = "5.16.2"
    junitVersion = "5.7.0"
    allureVersion = "2.13.5"
    floggerVersion = "0.4"
    javaOwnerVersion = "1.0.10"
}

dependencies {
    compile group: "com.google.flogger", name: "flogger-system-backend", version: "$floggerVersion"
    compile group: "com.google.flogger", name: "flogger", version: "$floggerVersion"
    compile group: "io.qameta.allure", name: "allure-selenide", version: "$allureVersion"
    compile group: "com.codeborne", name: "selenide", version: "$selenideVersion"
    compile group: 'org.aeonbits.owner', name: 'owner', version: "$javaOwnerVersion"
    testCompile group: "org.junit.jupiter", name: "junit-jupiter", version: "$junitVersion"
}

tasks.withType(Test) {
    useJUnitPlatform()

/*    testLogging {
        showStandardStreams = true
        events "passed", "failed"
        exceptionFormat = "full"
        displayGranularity = 4
    }*/

    testlogger {
        theme 'mocha'
        showExceptions true
        showStackTraces true
        showFullStackTraces false
        showCauses true
        slowThreshold 2000
        showSummary true
        showSimpleNames false
        showPassed true
        showSkipped true
        showFailed true
        showStandardStreams true
        showPassedStandardStreams true
        showSkippedStandardStreams true
        showFailedStandardStreams true
        logLevel 'lifecycle'
    }

    retry {
        maxRetries = 1
        maxFailures = 10
        failOnPassedAfterRetry = false
    }

    systemProperties["junit.jupiter.execution.parallel.enabled"] = true
    systemProperties["junit.jupiter.execution.parallel.config.strategy"] = "fixed"
    systemProperties["junit.jupiter.execution.parallel.config.fixed.parallelism"] = "$thread"
    systemProperties["site.locale"] = "$locale"
}

allure {
    version = "$allureVersion"
    aspectjweaver = true
    autoconfigure = true
    configuration = "compile"

    useJUnit5 {
        version = "$allureVersion"
    }
}

task testFirefox(type: Test) {
    systemProperties["browser"] = "firefox.$launch"
}

task testChrome(type: Test) {
    systemProperties["browser"] = "chrome.$launch"
}